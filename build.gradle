plugins {
   id "com.gradle.plugin-publish" version "0.9.9"
   id "pl.allegro.tech.build.axion-release" version "1.8.1"
   id "co.riiid.gradle" version '0.4.2'
   id 'groovy'
   id 'java-gradle-plugin'
   id 'com.redpillanalytics.gradle-analytics' version "0.1.2"
}

scmVersion {

   tag {
      prefix = 'v'
      versionSeparator = ''
   }
   ignoreUncommittedChanges = false
}

//set Gradle version to SCM Version
project.version = scmVersion.version

// configure the upload to GitHub releases
github {

   token = githubToken
   owner = 'RedPillAnalytics'
   repo = 'gradle-analytics'
   tagName = "v${version}"
   assets = fileTree(dir: buildDir, includes: ["**/*${version}*", "**/*${version}*.jar"]).toList()

}

test {
   ignoreFailures true
}

dependencies {

   compile gradleApi()
   compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.12'
   compile group: 'org.slf4j', name: 'slf4j-simple', version: '+'

   compile group: 'com.google.code.gson', name: 'gson', version: '+'
   compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '+'

   compile group: 'com.amazonaws', name: 'aws-java-sdk-kinesis', version: '+'
   compile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '+'

   compile 'com.google.cloud:google-cloud-pubsub:+'
   compile 'com.google.cloud:google-cloud-storage:+'

   testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
}

// Default artifact naming.
group = 'com.redpillanalytics'

repositories {
   jcenter()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
}

pluginBundle {

   website = 'http://redpillanalytics.com/'
   vcsUrl = 'https://github.com/RedPillAnalytics/gradle-analytics/'

   plugins {

      gradleAnalytics {
         id = 'com.redpillanalytics.gradle-analytics'
         displayName = 'com.redpillanalytics.gradle-analytics'
         description = "A plugin for publishing data from Gradle builds to a variety of cloud and on-premises data sinks."
         tags = ['analytics','data-sinks']
         version = project.version
      }
   }
}

task uploadGroovydoc {

   description = 'Upload Groovydocs to s3.'
   group = 'documentation'

   doLast {

      exec {

         executable 'aws'

         args 's3', 'sync', groovydoc.destinationDir.getPath(), "s3://gradle-analytics/documentation/$version/"
      }

      exec {

         executable 'aws'

         args 's3', 'sync', groovydoc.destinationDir.getPath(), "s3://gradle-analytics/documentation/latest/"
      }
   }

   dependsOn groovydoc
}