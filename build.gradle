plugins {
   id 'groovy'
   id 'java-gradle-plugin'
   id 'build-dashboard'
   id "com.gradle.plugin-publish" version "0.12.0"
   id "pl.allegro.tech.build.axion-release" version "1.11.0"
   id "com.github.ben-manes.versions" version "0.33.0"
   id "org.dvaske.gradle.git-build-info" version "0.9"
   id "com.redpillanalytics.gradle-properties" version "0.1.9"
   id "com.github.breadmoirai.github-release" version "2.2.12"
   id 'org.unbroken-dome.test-sets' version '3.0.1'
   id 'com.adarshr.test-logger' version '2.1.0'
   id "com.avast.gradle.docker-compose" version "0.13.3"
   id "be.vbgn.ci-detect" version "0.3.0"
   id "com.redpillanalytics.gradle-analytics" version "1.3.8"
}

scmVersion {

   tag {
      prefix = 'v'
      versionSeparator = ''
   }
   ignoreUncommittedChanges = true
}

//set Gradle version to SCM Version
allprojects {
   project.version = scmVersion.version
}

// send analytics
analytics {
   organization = 'Red Pill Analytics'
   bq {
      devops {
         bucket = 'rpa-gradle-analytics'
         dataset = 'gradle_analytics'
      }
   }
}

testSets {
   docs
   amazonTest
   googleTest
   kafkaTest
}

githubRelease {

   token = githubToken
   owner = 'RedPillAnalytics'
   repo = rootProject.name
   tagName = "v${version}"
   releaseAssets = fileTree(dir: buildDir, includes: ["**/*${version}*", "**/*${version}*.jar"]).toList()
}

dependencies {

   implementation gradleApi()
   implementation localGroovy()
   implementation 'org.slf4j:slf4j-simple:+'

   implementation "gradle.plugin.com.redpillanalytics:gradle-properties:0.1.9"

   implementation 'com.google.code.gson:gson:+'
   implementation 'org.eclipse.jgit:org.eclipse.jgit:+'
   implementation 'org.netbeans.external:org-apache-commons-io:+'

   // AWS
   implementation 'com.amazonaws:aws-java-sdk-kinesis:+'
   implementation 'com.amazonaws:aws-java-sdk-s3:+'

   // GCP
   implementation 'com.google.cloud:google-cloud-pubsub:+'
   implementation 'com.google.cloud:google-cloud-bigquery:+'
   implementation 'com.google.cloud:google-cloud-storage:+'

   // Kafka
   implementation 'org.apache.kafka:kafka-clients:+'
   implementation 'org.apache.avro:avro:+'

   // plugins
   implementation "be.vbgn.gradle:ci-detect-plugin:0.3.0"
   implementation "gradle.plugin.org.dvaske.gradle:git-build-info:+"
   implementation "org.ajoberstar.grgit:grgit-gradle:+"

   testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
   testImplementation "org.testcontainers:spock:1.14.3"
   testImplementation "org.testcontainers:kafka:1.14.3"
}

// Default artifact naming.
group = 'com.redpillanalytics'

repositories {
   jcenter()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
   maven {
      url "http://packages.confluent.io/maven/"
   }
}

gradlePlugin {
   plugins {
      gradleAnalytics {
         id = "com.redpillanalytics.${rootProject.name}"
         implementationClass = 'com.redpillanalytics.analytics.AnalyticsPlugin'
         description = "A plugin for publishing data from Gradle builds to a variety of data sinks."
      }
   }
}

pluginBundle {

   website = "https://github.com/RedPillAnalytics/${rootProject.name}/"
   vcsUrl  = "https://github.com/RedPillAnalytics/${rootProject.name}/"

   plugins {
      gradleAnalytics {
         id = "com.redpillanalytics.${rootProject.name}"
         displayName = rootProject.name
         tags = ['analytics', 'data-sinks', 'kafka', 'bigquery']
         version = project.version
      }
   }
}

task cleanJunit(type: Delete) {
   delete getTestResultsDir()
}

task cleanLibs(type: Delete) {
   delete getLibsDir()
}

// Options for all tests
task runAllTests {
   description 'Run all defined tests.'
   group 'verification'
}

tasks.withType(Test) {
   tasks.runAllTests.dependsOn it
   failFast true
   ignoreFailures true
   testLogging.showStandardStreams true
   systemProperty 'projectDir', temporaryDir
}

def versionUrl = "${docsBucket}/${rootProject.name}/${version}"
task publishVersionDocs(type: Exec) {
   description 'Publish version Groovydocs to S3.'
   group 'publishing'
   executable 'aws'
   args 's3', 'sync', groovydoc.destinationDir.getPath(), "s3://${versionUrl}"
   doLast {
      logger.warn "\nVersion docs: https://s3.amazonaws.com/${versionUrl}/index.html"
   }
   dependsOn groovydoc
}

def latestUrl = "${docsBucket}/${rootProject.name}/latest"
task publishLatestDocs(type: Exec) {
   description 'Publish latest Groovydocs to S3.'
   group 'publishing'
   executable 'aws'
   args 's3', 'sync', groovydoc.destinationDir.getPath(), "s3://${latestUrl}"
   doLast {
      logger.warn "\nLatest docs: https://s3.amazonaws.com/${latestUrl}/index.html"
   }
   dependsOn groovydoc
}

task publish {
   description 'Custom publish task.'
   group 'publishing'
   dependsOn tasks.publishPlugins, tasks.publishVersionDocs, tasks.publishLatestDocs, tasks.githubRelease, tasks.build
}

tasks.githubRelease.mustRunAfter tasks.publishPlugins, tasks.build
