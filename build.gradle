plugins {
   id 'groovy'
   id 'java-gradle-plugin'
   id 'build-dashboard'
   id "com.gradle.plugin-publish" version "0.13.0"
   id "de.epitschke.gradle-file-versioning" version "1.1.0"
   id "com.github.ben-manes.versions" version "0.38.0"
   id "org.dvaske.gradle.git-build-info" version "0.9"
   id "com.redpillanalytics.gradle-properties" version "0.1.11"
   id "com.github.breadmoirai.github-release" version "2.2.12"
   id 'org.unbroken-dome.test-sets' version '3.0.1'
   id 'com.adarshr.test-logger' version '2.1.1'
   id "com.avast.gradle.docker-compose" version "0.14.0"
   id "be.vbgn.ci-detect" version "0.5.0"
}

testSets {
   docs
   amazonTest
   googleTest
   kafkaTest
}

githubRelease {
   token = githubToken
   owner = 'RedPillAnalytics'
   repo = rootProject.name
   releaseAssets = libsDir.listFiles()
}

dependencies {

   implementation gradleApi()
   implementation localGroovy()
   implementation 'org.slf4j:slf4j-simple:1.7.30'

   implementation "gradle.plugin.com.redpillanalytics:gradle-properties:0.1.11"

   implementation 'com.google.code.gson:gson:2.8.6'
   implementation 'org.eclipse.jgit:org.eclipse.jgit:5.11.0.202103091610-r'
   implementation 'org.netbeans.external:org-apache-commons-io:RELEASE113'

   // AWS
   implementation 'com.amazonaws:aws-java-sdk-kinesis:1.11.976'
   implementation 'com.amazonaws:aws-java-sdk-s3:1.11.976'

   // GCP
   implementation 'com.google.cloud:google-cloud-pubsub:1.112.0'
   implementation 'com.google.cloud:google-cloud-bigquery:1.127.8'
   implementation 'com.google.cloud:google-cloud-storage:1.113.14'

   // Kafka
   implementation 'org.apache.kafka:kafka-clients:2.7.0'
   implementation 'org.apache.avro:avro:1.10.1'

   // plugins
   implementation "be.vbgn.gradle:ci-detect-plugin:0.5.0"
   implementation "gradle.plugin.org.dvaske.gradle:git-build-info:+"
   implementation "org.ajoberstar.grgit:grgit-gradle:4.1.0"

   testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
   testImplementation "org.testcontainers:spock:1.15.2"
   testImplementation "org.testcontainers:kafka:1.15.2"
}

// Default artifact naming.
group = 'com.redpillanalytics'

java {
   toolchain {
      languageVersion = JavaLanguageVersion.of(8)
   }
}

repositories {
   jcenter()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
   maven {
      url "http://packages.confluent.io/maven/"
   }
}

gradlePlugin {
   plugins {
      gradleAnalytics {
         id = "com.redpillanalytics.${rootProject.name}"
         implementationClass = 'com.redpillanalytics.analytics.AnalyticsPlugin'
         description = "A plugin for publishing data from Gradle builds to a variety of data sinks."
      }
   }
}

pluginBundle {

   website = "https://github.com/RedPillAnalytics/${rootProject.name}/"
   vcsUrl  = "https://github.com/RedPillAnalytics/${rootProject.name}/"

   plugins {
      gradleAnalytics {
         id = "com.redpillanalytics.${rootProject.name}"
         displayName = rootProject.name
         tags = ['analytics', 'data-sinks', 'kafka', 'bigquery']
         version = project.version
      }
   }
}

task cleanJunit(type: Delete) {
   delete getTestResultsDir()
}

task cleanLibs(type: Delete) {
   delete getLibsDir()
}

// Options for all tests
task runAllTests {
   description 'Run all defined tests.'
   group 'verification'
}

tasks.withType(Test) {
   tasks.runAllTests.dependsOn it
   failFast true
   systemProperty 'projectDir', temporaryDir
}

task publish {
   description 'Custom publish task.'
   group 'publishing'
   dependsOn tasks.publishPlugins, tasks.githubRelease, tasks.build
}

tasks.githubRelease.mustRunAfter tasks.publishPlugins, tasks.build
