// added because the github-release plugin used implementation instead of api/compile for plugin dependencies
// An issue exists for this, so hopefully it will be corrected soon
buildscript {
   dependencies {
      classpath 'com.squareup.okhttp3:okhttp:3.12.0'
      classpath 'com.j256.simplemagic:simplemagic:1.10'
      classpath 'org.zeroturnaround:zt-exec:1.10'
   }
}

plugins {
   id "com.gradle.plugin-publish" version "0.10.0"
   id "pl.allegro.tech.build.axion-release" version "1.10.0"
   id "com.github.breadmoirai.github-release" version "2.2.3"
   id 'groovy'
   id 'java-gradle-plugin'
   id 'org.unbroken-dome.test-sets' version '2.0.3'
   id "com.github.ben-manes.versions" version "0.20.0"
   //id "com.redpillanalytics.gradle-analytics" version "1.1.10"
   //id "com.selesse.git.changelog" version "0.3.0"
   //id "com.gorylenko.gradle-git-properties" version "2.0.0"
   //id "com.redpillanalytics.gradle-analytics" version "1.1.19"
   id "org.dvaske.gradle.git-build-info" version "0.9"
}

// send analytics
//analytics {
//   organization = 'Red Pill Analytics'
//   sinks {
//      pubsub
//      gs {
//         prefix = 'rpa-gradle-analytics'
//      }
//      s3 {
//         prefix = 'rpa-gradle-analytics'
//      }
//   }
//}

scmVersion {

   tag {
      prefix = 'v'
      versionSeparator = ''
   }
   ignoreUncommittedChanges = false
}

//set Gradle version to SCM Version
allprojects {
   project.version = scmVersion.version
}

testSets {
//   kafkaTest
//   confluentTest
   amazonTest
   googleTest
}

githubRelease {

   token = githubToken
   owner = 'RedPillAnalytics'
   repo = 'gradle-analytics'
   tagName = "v${version}"
   releaseAssets = fileTree(dir: buildDir, includes: ["**/*${version}*", "**/*${version}*.jar"]).toList()
}

dependencies {

   compile gradleApi()
   compile localGroovy()
   compile group: 'org.slf4j', name: 'slf4j-simple', version: '+'

   compile group: 'com.google.code.gson', name: 'gson', version: '+'
   compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '+'

   compile group: 'com.amazonaws', name: 'aws-java-sdk-kinesis', version: '+'
   compile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '+'

   compile 'com.google.cloud:google-cloud-pubsub:+'

   // Kafka
   compile 'org.apache.kafka:kafka-clients:2.1.0'
   compile 'org.apache.avro:avro:1.8.2'

   compile 'com.google.cloud:google-cloud-storage:+'

   compile 'com.google.cloud:google-cloud-storage:+'

   compile "gradle.plugin.org.dvaske.gradle:git-build-info:0.9"

   testCompile 'org.spockframework:spock-core:1.2-groovy-2.5'
}

// Default artifact naming.
group = 'com.redpillanalytics'

repositories {
   jcenter()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
   maven {
      url releaseUrl
      authentication {
         awsIm(AwsImAuthentication)
      }
   }
   maven {
      url snapshotUrl
      authentication {
         awsIm(AwsImAuthentication)
      }
   }
   maven {
      url "http://packages.confluent.io/maven/"
   }
}

gradlePlugin {
   plugins {
      gradleAnalytics {
         id = 'com.redpillanalytics.gradle-analytics'
         implementationClass = 'com.redpillanalytics.analytics.AnalyticsPlugin'
      }
   }
}

pluginBundle {

   website = 'http://redpillanalytics.com/'
   vcsUrl = 'https://github.com/RedPillAnalytics/gradle-plugins/'

   plugins {
      gradleAnalytics {
         id = 'com.redpillanalytics.gradle-analytics'
         displayName = 'gradle-analytics'
         description = "A plugin for publishing data from Gradle builds to a variety of cloud and on-premises data sinks."
         tags = ['analytics', 'data-sinks']
         version = project.version
      }
   }
}

// Options for all tests
tasks.withType(Test) {
   ignoreFailures true
   testLogging.showStandardStreams true
}

task uploadGroovydoc {

   description = 'Upload Groovydocs to s3.'
   group = 'documentation'

   doLast {
      exec {
         executable 'aws'
         args 's3', 'sync', groovydoc.destinationDir.getPath(), "s3://rpa-documentation/gradle-analytics/$version/"
      }
      exec {
         executable 'aws'
         args 's3', 'sync', groovydoc.destinationDir.getPath(), "s3://rpa-documentation/gradle-analytics/latest/"
      }
   }

   dependsOn groovydoc
}

// Only execute certain tasks if we are on the master branch
//tasks.release.configure { onlyIf { gitBranchName == 'master' } }
//tasks.publishPlugins.configure { onlyIf { gitBranchName == 'master' } }
//tasks.githubRelease.configure { onlyIf { gitBranchName == 'master' } }

