plugins {
   id 'groovy'
   id 'java-gradle-plugin'
   id 'build-dashboard'
   id "com.gradle.plugin-publish" version "0.20.0"
   id "de.epitschke.gradle-file-versioning" version "1.3.0"
   id "com.github.ben-manes.versions" version "0.42.0"
   id "org.dvaske.gradle.git-build-info" version "0.9"
   id "com.redpillanalytics.gradle-properties" version "1.0.5"
   id "com.github.breadmoirai.github-release" version "2.2.12"
   id 'org.unbroken-dome.test-sets' version '4.0.0'
   id 'com.adarshr.test-logger' version '3.2.0'
   id "com.avast.gradle.docker-compose" version "0.15.2"
}

testSets {
   docs
   amazonTest
   googleTest
   kafkaTest
}

githubRelease {
   token project.findProperty('githubToken').toString()
   owner 'RedPillAnalytics'
   repo rootProject.name
   overwrite true
   releaseAssets jar.destinationDirectory.files()
}

dependencies {

   implementation gradleApi()
   implementation 'org.codehaus.groovy:groovy:3.0.10'

   implementation "gradle.plugin.com.redpillanalytics:gradle-properties:1.0.4"

   implementation 'com.google.code.gson:gson:2.9.0'
   implementation 'org.eclipse.jgit:org.eclipse.jgit:6.1.0.202203080745-r'
   implementation 'org.netbeans.external:org-apache-commons-io:RELEASE113'

   // AWS
   implementation 'com.amazonaws:aws-java-sdk-kinesis:1.12.198'
   implementation 'com.amazonaws:aws-java-sdk-s3:1.12.198'

   // GCP
   implementation 'com.google.cloud:google-cloud-pubsub:1.116.3'
   implementation 'com.google.cloud:google-cloud-bigquery:2.10.7'
   implementation 'com.google.cloud:google-cloud-storage:2.6.0'

   // Kafka
   implementation 'org.apache.kafka:kafka-clients:3.0.1'
   implementation 'org.apache.avro:avro:1.11.0'

   // plugins
   implementation "be.vbgn.gradle:ci-detect-plugin:0.5.0"
   implementation "gradle.plugin.org.dvaske.gradle:git-build-info:+"
   implementation "org.ajoberstar.grgit:grgit-gradle:5.0.0"

   testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
   testImplementation "org.testcontainers:spock:1.17.1"
   testImplementation "org.testcontainers:kafka:1.17.1"
}

// Default artifact naming.
group = 'com.redpillanalytics'

java {
   toolchain {
      languageVersion = JavaLanguageVersion.of(11)
   }
}

repositories {
   mavenCentral()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
   maven {
      url "https://packages.confluent.io/maven/"
   }
}

gradlePlugin {
   plugins {
      gradleAnalytics {
         id = "com.redpillanalytics.${rootProject.name}"
         implementationClass = 'com.redpillanalytics.analytics.AnalyticsPlugin'
         description = "A plugin for publishing data from Gradle builds to a variety of data sinks."
      }
   }
}

pluginBundle {

   website = "https://github.com/RedPillAnalytics/${rootProject.name}/"
   vcsUrl  = "https://github.com/RedPillAnalytics/${rootProject.name}/"

   plugins {
      gradleAnalytics {
         id = "com.redpillanalytics.${rootProject.name}"
         displayName = rootProject.name
         tags = ['analytics', 'data-sinks', 'kafka', 'bigquery']
         version = project.version
      }
   }
}

task cleanJunit(type: Delete) {
   delete getTestResultsDir()
}

task cleanLibs(type: Delete) {
   delete jar.destinationDirectory.files()
}

// Options for all tests
task runAllTests {
   description 'Run all defined tests.'
   group 'verification'
}

tasks.withType(Test) {
   tasks.runAllTests.dependsOn it
   failFast true
   systemProperty 'projectDir', temporaryDir
   useJUnitPlatform()
}

task publish {
   description 'Custom publish task.'
   group 'publishing'
   dependsOn tasks.publishPlugins, tasks.githubRelease, tasks.build
}

tasks.githubRelease.mustRunAfter tasks.publishPlugins, tasks.build
