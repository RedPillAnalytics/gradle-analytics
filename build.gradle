plugins {
   id "com.gradle.plugin-publish" version "0.10.0"
   id "pl.allegro.tech.build.axion-release" version "1.9.3"
   id "com.github.breadmoirai.github-release" version "2.0.1"
   id 'groovy'
   id 'java-gradle-plugin'
   id 'org.unbroken-dome.test-sets' version '2.0.2'
   id "com.github.johnrengelman.shadow" version "4.0.2"
   id "com.github.ben-manes.versions" version "0.20.0"
   id "com.redpillanalytics.gradle-analytics" version "1.1.10"
}

// send analytics
//analytics {
//   organization = 'Red Pill Analytics'
//   sinks {
//      pubsub
//      gs {
//         prefix = 'rpa-gradle-analytics'
//      }
//      s3 {
//         prefix = 'rpa-gradle-analytics'
//      }
//   }
//}

scmVersion {

   tag {
      prefix = 'v'
      versionSeparator = ''
   }
   ignoreUncommittedChanges = false
}

//set Gradle version to SCM Version
allprojects {
   project.version = scmVersion.version
}

testSets {
//   kafkaTest
//   confluentTest
   amazonTest
   googleTest
}

// configure the upload to GitHub releases
//github {
//
//   token = githubToken
//   owner = 'RedPillAnalytics'
//   repo = 'gradle-plugins'
//   tagName = "v${version}"
//   assets = fileTree(dir: buildDir, includes: ["**/*${version}*", "**/*${version}*.jar"]).toList()
//
//}

githubRelease {

   token = githubToken
   owner = 'RedPillAnalytics'
   repo = 'gradle-analytics'
   tagName = "v${version}"
   releaseAssets = fileTree(dir: buildDir, includes: ["**/*${version}*", "**/*${version}*.jar"]).toList()
}

dependencies {

   implementation gradleApi()
   implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.15'
   implementation group: 'org.slf4j', name: 'slf4j-simple', version: '+'

   // gradle-utilities
   implementation 'com.redpillanalytics:gradle-utilities:+'

   implementation group: 'com.google.code.gson', name: 'gson', version: '+'
   implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '+'

   implementation group: 'com.amazonaws', name: 'aws-java-sdk-kinesis', version: '+'
   implementation group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '+'

   implementation 'com.google.cloud:google-cloud-pubsub:+'
   implementation 'com.google.cloud:google-cloud-storage:+'

   // Kafka
   implementation 'org.apache.kafka:kafka-clients:2.0.0'
   implementation 'org.apache.avro:avro:1.8.2'
//   implementation ('io.confluent:kafka-avro-serializer:3.2.1') {
//      exclude group: "org.slf4j"
//   }
   //implementation group: 'tech.allegro.schema.json2avro', name: 'converter', version: '0.2.6'

   testImplementation('org.spockframework:spock-core:1.1-groovy-2.4')
}

// Default artifact naming.
group = 'com.redpillanalytics'

repositories {
   jcenter()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
   maven {
      url releaseUrl
      authentication {
         awsIm(AwsImAuthentication)
      }
   }
   maven {
      url snapshotUrl
      authentication {
         awsIm(AwsImAuthentication)
      }
   }
   maven {
      url "http://packages.confluent.io/maven/"
   }
}

gradlePlugin {
   plugins {
      gradleAnalytics {
         id = 'com.redpillanalytics.gradle-analytics'
         implementationClass = 'com.redpillanalytics.analytics.AnalyticsPlugin'
      }
   }
}

pluginBundle {

   website = 'http://redpillanalytics.com/'
   vcsUrl = 'https://github.com/RedPillAnalytics/gradle-plugins/'

   plugins {

      gradleAnalytics {
         id = 'com.redpillanalytics.gradle-analytics'
         displayName = 'gradle-analytics'
         description = "A plugin for publishing data from Gradle builds to a variety of cloud and on-premises data sinks."
         tags = ['analytics', 'data-sinks']
         version = project.version
      }
   }
}

// Options for all tests
tasks.withType(Test) {
   ignoreFailures true
   testLogging.showStandardStreams true
}

task uploadGroovydoc {

   description = 'Upload Groovydocs to s3.'
   group = 'documentation'

   doLast {

      exec {
         executable 'aws'
         args 's3', 'sync', groovydoc.destinationDir.getPath(), "s3://rpa-documentation/gradle-analytics/$version/"
      }

      exec {
         executable 'aws'
         args 's3', 'sync', groovydoc.destinationDir.getPath(), "s3://rpa-documentation/gradle-analytics/latest/"
      }
   }

   dependsOn groovydoc
}

//customize ShadowJar
jar.enabled = false

shadowJar {
   classifier = ''
   zip64 = true
}

tasks.build.dependsOn tasks.shadowJar
